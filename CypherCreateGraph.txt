//the "file:///loan_full.csv" must be placed in the import folder of the Neo4j database, e.g.:
//C:\Users\20175070\.Neo4jDesktop\neo4jDatabases\database-9d1700fa-03f9-48e7-b8ad-6b7c25890ee2\installation-3.5.4\import


//create unique constraints for event, case and resource ids (perform one by one)
CREATE CONSTRAINT ON (e:Event) ASSERT e.name IS UNIQUE;
CREATE CONSTRAINT ON (c:Case) ASSERT c.name IS UNIQUE;
CREATE CONSTRAINT ON (r:Resource) ASSERT r.name IS UNIQUE;

//1 create case nodes with case attributes (Added 31509 labels, created 31509 nodes, set 157545 properties, completed after 15973 ms.)
USING PERIODIC COMMIT 1000
LOAD CSV WITH HEADERS FROM "file:///loan_full.csv" as line
WITH line.case as case, 
	line.LoanGoal as loanGoal, 
	line.ApplicationType as applicationType, 	
	line.RequestedAmount as requestedAmount,
	line.FirstWithdrawalAmount as firstAmount,
	line.NumberOfTerms as n_Terms,
	line.variant_index as vIndex
MERGE (c:Case {name: case, variant: vIndex, loangoal: loanGoal, applicationtype: applicationType, requestedamount: toInteger(requestedAmount)})

//2 create event nodes with event attributes and relationships to cases (Added 561671 labels, created 561671 nodes, set 6029756 properties, created 561671 relationships, completed after 38875 ms.)
USING PERIODIC COMMIT 1000
LOAD CSV WITH HEADERS FROM "file:///loan_full.csv" as line
WITH line.case as case, 
	line.EventID as event,  
	line.EventOrigin as eventClass,
	line.event as activity,
	line.Action as action,
	line.startTime as startTime,	
	line.completeTime as completeTime,
	line.OfferID as offer,
	line.case_index as cIndex,
	line.offer_index as oIndex,
	line.application_index as aIndex,
	line.workflow_index as wIndex,
    line.sY as sY, line.sD as sD, line.sM as sM, line.sHH as sHH, line.sMM as sMM, line.sSS as sSS, line.sMS as sMS,
	line.cY as cY, line.cD as cD, line.cM as cM, line.cHH as cHH, line.cMM as cMM, line.cSS as cSS, line.cMS as cMS
MATCH (c:Case {name: case})
CREATE (e:Event {name: event, 
	starttime: localdatetime({year:toInteger(sY), month:toInteger(sM), day:toInteger(sD), hour:toInteger(sHH), minute:toInteger(sMM), second:toInteger(sSS), microsecond:toInteger(sMS)}), 
	completetime: localdatetime({year:toInteger(cY), month:toInteger(cM), day:toInteger(cD), hour:toInteger(cHH), minute:toInteger(cMM), second:toInteger(cSS), microsecond:toInteger(cMS)}), 
	activity: activity, class: eventClass, action: action, offerid: offer, caseindex: toInteger(cIndex), offerindex: toInteger(oIndex), applicationindex: toInteger(aIndex), workflowindex: toInteger(wIndex)})
CREATE (e) -[:EVENT_TO_CASE]-> (c)

//3 create nodes for each resource and edges to event nodes (Added 145 labels, created 145 nodes, set 145 properties, created 561671 relationships, completed after 17200 ms.)
USING PERIODIC COMMIT 1000
LOAD CSV WITH HEADERS FROM "file:///loan_full.csv" as line
WITH line.resource as resource, line.EventID as event
MATCH (e:Event {name: event})
MERGE (r:Resource {name: resource})
CREATE (r) -[:RESOURCE_TO_EVENT]-> (e)



//4 create directly follows relationships for events (Set 530162 properties, created 530162 relationships, completed after 22645 ms.)
MATCH (e1:Event) --> (c:Case) <-- (e2:Event)
WHERE e2.caseindex - e1.caseindex = 1
CREATE (e2) -[:DF {timebetween: duration.between(e1.completetime, e2.starttime) }]-> (e1)

//5 create offers, relationships offer -> case (Added 42995 labels, created 42995 nodes, set 257970 properties, created 42995 relationships, completed after 10830 ms.)
USING PERIODIC COMMIT 1000
LOAD CSV WITH HEADERS FROM "file:///loan_full.csv" as line
WITH line.case as case, 
	line.event as activity,
	line.RequestedAmount as requestedAmount,
	line.FirstWithdrawalAmount as firstAmount,
	line.NumberOfTerms as n_Terms,
	line.OfferID as offer,
	line.resource as resource,
	line.MonthlyCost as monthlyCost,
	line.CreditScore as creditScore,
	line.OfferedAmount as offeredAmount
MATCH(c:Case {name: case}), (r:Resource {name: resource})
WHERE activity = "O_Created"
CREATE (o:Offer {name: offer, firstamount: firstAmount, n_terms: n_Terms, monthlycost: monthlyCost, offeredamount: offeredAmount, creditscore: creditScore })
CREATE (o) -[:OFFER_TO_CASE]-> (c)

//6 create relationships offer -> event to associate (Offer)events with offers (Created 193849 relationships, completed after 1299 ms.)
MATCH (e:Event), (o:Offer)
WHERE o.name = e.offerid AND e.offerindex > 0  
CREATE (e) -[:EVENT_TO_OFFER]-> (o)

//7 create application nodes,  (Added 31509 labels, created 31509 nodes, set 126036 properties, completed after 298 ms.) 
MATCH (c:Case)
CREATE (a:Application {name: c.name, loangoal: c.loangoal, applicationtype: c.applicationtype, requestedamount: c.requestedamount})

//8 create relationships application -> case (Created 31509 relationships, completed after 423 ms.)
MATCH (c:Case), (a:Application)
WHERE c.name = a.name
CREATE (a) -[:APPLICATION_TO_CASE]-> (c)

//9 create relationships application -> event to associate (application)events with application (Created 239595 relationships, completed after 1209 ms.)
MATCH (e:Event) -[:EVENT_TO_CASE]-> (c:Case) <-[:APPLICATION_TO_CASE]- (a:Application) 
WHERE e.class = "Application"
CREATE (e) -[:EVENT_TO_APPLICATION]->  (a)

//10 create workflow nodes,  (Added 31509 labels, created 31509 nodes, set 31509 properties, completed after 152 ms.)
MATCH (c:Case)
CREATE (w:Workflow {name: c.name})

//11 relationships workflow -> case(Created 31509 relationships, completed after 383 ms.)
MATCH (c:Case), (w:Workflow)
WHERE c.name = w.name
CREATE (w) -[:WORKFLOW_TO_CASE]-> (c)

//12 create relationships workflow -> event to associate (workflow)events with workflow (Created 128227 relationships, completed after 813 ms.)
MATCH (e:Event) -[:EVENT_TO_CASE]-> (c:Case) <-[:WORKFLOW_TO_CASE]- (w:Workflow) 
WHERE e.class = "Workflow"
CREATE (e) -[:EVENT_TO_WORKFLOW]->  (w)


//13 create event chain of offers (Set 150854 properties, created 150854 relationships, completed after 2248 ms.)
MATCH (e1:Event) -[:EVENT_TO_OFFER]-> (o:Offer) <-[:EVENT_TO_OFFER]- (e2:Event)
WHERE e2.offerindex - e1.offerindex = 1
CREATE (e2) -[:O_DF {timebetween: duration.between(e1.completetime, e2.starttime) }]-> (e1)

//14 create application based directly follows relationships (Set 208086 properties, created 208086 relationships, completed after 10294 ms.)
MATCH (e1:Event) -[:EVENT_TO_CASE]-> (c:Case) <-[:EVENT_TO_CASE]- (e2:Event)
WHERE e2.applicationindex - e1.applicationindex = 1 AND e1.applicationindex > 0
CREATE (e1) <-[:A_DF {timebetween: duration.between(e1.completetime, e2.starttime) }]- (e2)

//15 create workflow based directly follows relationships (Set 96727 properties, created 96727 relationships, completed after 5218 ms.)
MATCH (e1:Event) -[:EVENT_TO_CASE]-> (c:Case) <-[:EVENT_TO_CASE]- (e2:Event)
WHERE e2.workflowindex - e1.workflowindex = 1 AND e1.workflowindex > 0
CREATE (e1) <-[:W_DF {timebetween: duration.between(e1.completetime, e2.starttime) }]- (e2)

//16 create case-based handover of work relationships (Created 11181 relationships, completed after 15588 ms.)
MATCH (r1:Resource) -[:RESOURCE_TO_EVENT]-> (e1:Event) <-[:DF]- (e2:Event) <-[:RESOURCE_TO_EVENT]- (r2:Resource)
MERGE (r1) -[:HOW]-> (r2)

//17 create offer-based handover of work relationships (Created 6313 relationships, completed after 3739 ms.)
MATCH (r1:Resource) -[:RESOURCE_TO_EVENT]-> (e1:Event) <-[:O_DF]- (e2:Event) <-[:RESOURCE_TO_EVENT]- (r2:Resource)
MERGE (r1) -[:O_HOW]-> (r2)

//18 create application-based handover of work relationships (Created 8205 relationships, completed after 5716 ms.)
MATCH (r1:Resource) -[:RESOURCE_TO_EVENT]-> (e1:Event) <-[:A_DF]- (e2:Event) <-[:RESOURCE_TO_EVENT]- (r2:Resource)
MERGE (r1) -[:A_HOW]-> (r2)

//19 create workflow-based handover of work relationships (Created 9353 relationships, completed after 3591 ms.)
MATCH (r1:Resource) -[:RESOURCE_TO_EVENT]-> (e1:Event) <-[:W_DF]- (e2:Event) <-[:RESOURCE_TO_EVENT]- (r2:Resource)
MERGE (r1) -[:W_HOW]-> (r2)

//20 clean up temporary data (Set 974717 properties, completed after 1394 ms.)
MATCH (e:Event)
REMOVE e.offerid, e.offerindex 


